error.md

# Errors 
  
## Error Set Type 
  
An `error` set is like an `enum`. However, each error name across the entire compilation gets assigned an unsigned integer greater than 0. You are allowed to declare the same `error` name more than once, and if you do, it gets assigned the same integer value.
  
The number of unique error values across the entire compilation should determine the size of the error set type. However right now it is hard coded to be a `u16`.
  
You can coerce an error from a subset to a superset.
  
**ZigC**
```
import std;

error FileOpenError {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
}

error AllocationError {
    OutOfMemory,
}

test "coerce subset to superset" {
    const err = foo(AllocationError.OutOfMemory);
    try std.testing.expect(err == FileOpenError.OutOfMemory);
}

FileOpenError foo(AllocationError err)  {
    return err;
}
```
  
**Zig**
```
const std = @import("std");

const FileOpenError = error {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
};

const AllocationError = error {
    OutOfMemory,
};

test "coerce subset to superset" {
    const err = foo(AllocationError.OutOfMemory);
    try std.testing.expect(err == FileOpenError.OutOfMemory);
}

fn foo(err: AllocationError) FileOpenError {
    return err;
}
```
 
**ZigC**
```
error FileOpenError {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
}

error AllocationError {
    OutOfMemory,
}

test "coerce superset to subset" {
    foo(FileOpenError.OutOfMemory) catch {};
}

AllocationError foo(FileOpenError err)  {
    return err; // Error!! coerce to superset is not allowed
}

// a shortcut for declaring an error set with only 1 value, and then getting that value
const err = error.FileNotFound;
const err = (error {FileNotFound}).FileNotFound;
```
  
**Zig**
```
const FileOpenError = error {
    AccessDenied,
    OutOfMemory,
    FileNotFound,
};

const AllocationError = error {
    OutOfMemory,
};

test "coerce superset to subset" {
    foo(FileOpenError.OutOfMemory) catch {};
}

fn foo(err: FileOpenError) AllocationError {
    return err; // Error!! coerce to superset is not allowed
}

// a shortcut for declaring an error set with only 1 value, and then getting that value
const err = error.FileNotFound;
const err = (error {FileNotFound}).FileNotFound;
```
  
## The Global Error Set 
  
`anyerror` refers to the global error set. This is the error set that contains all errors in the entire compilation unit. It is a superset of all other error sets and a subset of none of them.
  
You can coerce any error set to the global one, and you can explicitly cast an error of the global error set to a non-global one. This inserts a language-level assert to make sure the error value is in fact in the destination error set.
   
The global error set should generally be avoided because it prevents the compiler from knowing what errors are possible at compile-time. Knowing the error set at compile-time is better for generated documentation and helpful error messages, such as forgetting a possible error value in a switch.
  
## Error Union Type 
  
An error set type and normal type can be combined with the `!` binary operator to form an error union type. You are likely to use an error union type more often than an error set type by itself.
  
Here is a function to parse a string into a 64-bit integer.
  
**ZigC**
```
import std;
const maxInt = std.math.maxInt;

public u64! parseU64(const u8[], u8 radix) {
    u64 x = 0;

    for c in buf {
        const digit = charToDigit(c);

        if (digit >= radix) {
            return error.InvalidChar; // coerced to u64!anyerror
        }

        // x *= radix
        if (@mulWithOverflow(u64, x, radix, &x)) {
            return error.Overflow; // coerced to u64!anyerror
        }

        // x += digit
        if (@addWithOverflow(u64, x, digit, &x)) {
            return error.Overflow; // coerced to u64!anyerror
        }
    }

    return x; // coerced to u64!anyerror
}

u8 charToDigit(u8 c) {
    return switch (c) {
        case '0' ... '9': c - '0';
        case 'A' ... 'Z': c - 'A' + 10;
        case 'a' ... 'z': c - 'a' + 10;
        default: maxInt(u8);
    };
}

test "parse u64" {
    const result = try parseU64("1234", 10);
    try std.testing.expect(result == 1234);
}
```
  
**Zig**
```
const std = @import("std");
const maxInt = std.math.maxInt;

pub fn parseU64(buf: []const u8, radix: u8) !u64 {
    var x: u64 = 0;

    for (buf) |c| {
        const digit = charToDigit(c);

        if (digit >= radix) {
            return error.InvalidChar;
        }

        // x *= radix
        if (@mulWithOverflow(u64, x, radix, &x)) {
            return error.Overflow;
        }

        // x += digit
        if (@addWithOverflow(u64, x, digit, &x)) {
            return error.Overflow;
        }
    }

    return x;
}

fn charToDigit(c: u8) u8 {
    return switch (c) {
        '0' ... '9' => c - '0',
        'A' ... 'Z' => c - 'A' + 10,
        'a' ... 'z' => c - 'a' + 10,
        else => maxInt(u8),
    };
}

test "parse u64" {
    const result = try parseU64("1234", 10);
    try std.testing.expect(result == 1234);
}
```
  
## catch 
  
If you want to provide a default value, you can use the catch binary operator.
  
**ZigC**
```
const parseU64 = @import("error_union_parsing_u64.zc").parseU64;

void doAThing(u8[] str) {
    const number = parseU64(str, 10) catch 13; // on error, take 13 as default value
    _ = number; // ...
}
```
  
The type of the right hand side of the binary catch operator must match the unwrapped error union type, or be of type `noreturn`.
  
**Zig**
```
const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) void {
    const number = parseU64(str, 10) catch 13;
    _ = number; // ...
}
```
  
## try 
  
**ZigC**
```
const parseU64 = @import("error_union_parsing_u64.zc").parseU64;

void! doAThing(u8[] str) {
    const number = parseU64(str, 10) catch (err) return err;
    _ = number; // ...
}

void! doAThing(u8[] str) {
    const number = try parseU64(str, 10);  // on error return it
    _ = number; // ...
}
```
  
`try` evaluates an error union expression. If it is an error, it returns from the current function with the same error.
   
**Zig**
```
const parseU64 = @import("error_union_parsing_u64.zig").parseU64;

fn doAThing(str: []u8) !void {
    const number = parseU64(str, 10) catch |err| return err;
    _ = number; // ...
}

fn doAThing(str: []u8) !void {
    const number = try parseU64(str, 10); // on error return it
    _ = number; // ...
}
```
  
If absolutely certain that no error returns, `catch unreachable` can be used.
  
```
const number = parseU64("1234", 10) catch unreachable;
```
  

## catch / switch
  
**ZigC**
```
void doAThing(u8[] str) {
    if (parseU64(str, 10) 'number) { // quoted number is non error u64
        doSomethingWithNumber(number);
    } catch (err) switch (err) {
        case error.Overflow: {
            // handle overflow...
        };
        case error.InvalidChar: unreachable;
    }
}
```
  
**Zig**
```
fn doAThing(str: []u8) void {
    if (parseU64(str, 10)) |number| {
        doSomethingWithNumber(number);
    } else |err| switch (err) {
        error.Overflow => {
            // handle overflow...
        },
        error.InvalidChar => unreachable,
    }
}
```
  
## errdefer
  
Call deferred block, only if the function returns with an error.
  
**ZigC**
```
Foo! createFoo(int param) {
    const foo = try tryToAllocateFoo(); // on error return it
    errdefer deallocateFoo(foo); // register for error case

    const tmp_buf = allocateTmpBuffer() orelse return error.OutOfMemory; // if error, call errdefered block here

    defer deallocateTmpBuffer(tmp_buf);

    if (param > 1337) return error.InvalidParam; // call errdefered block here

    return foo; // call defered block here
}
```
  
**Zig**
```
fn createFoo(param: i32) !Foo {
    const foo = try tryToAllocateFoo();
    errdefer deallocateFoo(foo);

    const tmp_buf = allocateTmpBuffer() orelse return error.OutOfMemory;

    defer deallocateTmpBuffer(tmp_buf);

    if (param > 1337) return error.InvalidParam;

    return foo;
}
```
  
## Warning: errdefer scope slip-ups
  
**ZigC**
```
import std;
const Allocator = std.mem.Allocator;

struct Foo {
    u32 data;
}

Foo*! tryToAllocateFoo(Allocator allocator) {
    return allocator.create(Foo);
}

void deallocateFoo(Allocator allocator, Foo* foo) {
    allocator.destroy(foo);
}

u32! getFooData() {
    return 666;
}

Foo*! createFoo(Allocator allocator, int param) {
    const foo = getFoo: {
        Foo* foo = try tryToAllocateFoo(allocator); // on error, return
        errdefer deallocateFoo(allocator, foo); 

        foo.data = try getFooData(); // possible error

        break :getFoo foo;
    };  // errdefer scope out
    // errdefer deallocateFoo(allocator, foo); // this line is necessary

    if (param > 1337) return error.InvalidParam; // errdefered deallocateFoo() not called, out of scope

    return foo;
}

test "createFoo" {
    try std.testing.expectError(error.InvalidParam, createFoo(std.testing.allocator, 2468));
} // Error! memory leak
```
  
**Zig**
```
const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: u32,
};

fn tryToAllocateFoo(allocator: Allocator) !*Foo {
    return allocator.create(Foo);
}

fn deallocateFoo(allocator: Allocator, foo: *Foo) void {
    allocator.destroy(foo);
}

fn getFooData() !u32 {
    return 666;
}

fn createFoo(allocator: Allocator, param: i32) !*Foo {
    const foo = getFoo: {
        var foo = try tryToAllocateFoo(allocator);
        errdefer deallocateFoo(allocator, foo); 

        foo.data = try getFooData();

        break :getFoo foo;
    };
    // errdefer deallocateFoo(allocator, foo); // this line is necessary

    if (param > 1337) return error.InvalidParam; // errdefered block not called, out of scope

    return foo;
}

test "createFoo" {
    try std.testing.expectError(error.InvalidParam, createFoo(std.testing.allocator, 2468));
}
```
  
## Special warning: errdefer in loop
  
**ZigC**
```
import std;
const Allocator = std.mem.Allocator;

struct Foo {
    u32* data;
}

u32! getData() {
    return 666;
}

Foo[]! genFoos(Allocator allocator, size_t num) {
    Foo[] foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    for i, *foo in foos {
        foo.data = try allocator.create(u32);
        errdefer allocator.destroy(foo.data); // this expires every iteration

        if(i >= 3) return error.TooManyFoos; // destroys just one item, fourth one

        *foo.data = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
} // Error!!! memory leak detected
```
  
**Zig**
```
const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: *u32
};

fn getData() !u32 {
    return 666;
}

fn genFoos(allocator: Allocator, num: usize) ![]Foo {
    var foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    for(foos) |*foo, i| {
        foo.data = try allocator.create(u32);
        // This errdefer does not last between iterations
        errdefer allocator.destroy(foo.data);

        // The data for the first 3 foos will be leaked
        if(i >= 3) return error.TooManyFoos;

        foo.data.* = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
} // Error!! memory leak detected
```
  
## Proper errdefer for loops
  
**ZigC**
```
import std;
const Allocator = std.mem.Allocator;

struct Foo {
    u32* data;
}

u32! getData() {
    return 666;
}

Foo[]! genFoos(Allocator allocator, size_t num) {
    Foo[] foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    // Used to track how many foos have been initialized
    // (including their data being allocated)
    size_t num_allocated = 0;
    errdefer for foo in foos[0..num_allocated] {
        allocator.destroy(foo.data);
    };
    for i, *foo in foos {
        foo.data = try allocator.create(u32);
        num_allocated += 1;

        if(i >= 3) return error.TooManyFoos; // errdefered for loop called here

        *foo.data = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
}
```
  

**Zig**
```
const std = @import("std");
const Allocator = std.mem.Allocator;

const Foo = struct {
    data: *u32
};

fn getData() !u32 {
    return 666;
}

fn genFoos(allocator: Allocator, num: usize) ![]Foo {
    var foos = try allocator.alloc(Foo, num);
    errdefer allocator.free(foos);

    // Used to track how many foos have been initialized
    // (including their data being allocated)
    var num_allocated: usize = 0;
    errdefer for(foos[0..num_allocated]) |foo| {
        allocator.destroy(foo.data);
    };
    for(foos) |*foo, i| {
        foo.data = try allocator.create(u32);
        num_allocated += 1;

        if(i >= 3) return error.TooManyFoos;

        foo.data.* = try getData();
    }

    return foos;
}

test "genFoos" {
    try std.testing.expectError(error.TooManyFoos, genFoos(std.testing.allocator, 5));
}
```
  
## comptime reflection on error union
  
**ZigC**
```
from std.testing import expect;

test "error union" {
    int!anyerror foo = undefined;

    // Coerce from child type of an error union:
    foo = 1234;

    // Coerce from an error set:
    foo = error.SomeError;

    // Use compile-time reflection to access the payload type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.payload == int);

    // Use compile-time reflection to access the error set type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.error_set == anyerror);
}
```
   
**Zig**
```
const expect = @import("std").testing.expect;

test "error union" {
    var foo: anyerror!i32 = undefined;

    // Coerce from child type of an error union:
    foo = 1234;

    // Coerce from an error set:
    foo = error.SomeError;

    // Use compile-time reflection to access the payload type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.payload == i32);

    // Use compile-time reflection to access the error set type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.error_set == anyerror);
}
```
  
If you really want to ignore the error, you can add `catch unreachable` and get the added benefit of crashing in Debug and ReleaseSafe modes if your assumption was wrong.
  
## error union
  
An error union is created with the `!` binary operator. You can use compile-time reflection to access the child type of an error union.
  
**ZigC**
```
from std.testing import expect;

test "error union" {
    int!anyerror foo = undefined;

    // Coerce from child type of an error union:
    foo = 1234;

    // Coerce from an error set:
    foo = error.SomeError;

    // Use compile-time reflection to access the payload type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.payload == i32);

    // Use compile-time reflection to access the error set type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.error_set == anyerror);
}
```
  
**Zig**
```
const expect = @import("std").testing.expect;

test "error union" {
    var foo: anyerror!i32 = undefined;

    // Coerce from child type of an error union:
    foo = 1234;

    // Coerce from an error set:
    foo = error.SomeError;

    // Use compile-time reflection to access the payload type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.payload == i32);

    // Use compile-time reflection to access the error set type of an error union:
    comptime try expect(@typeInfo(@TypeOf(foo)).ErrorUnion.error_set == anyerror);
}
```
   
## Merging Error Sets 
  
Use the `||` operator to merge two error sets together. 
  
**ZigC**
```
error A {
    NotDir,

    /// A doc comment
    PathNotFound,
}
error B {
    OutOfMemory,

    /// B doc comment
    PathNotFound,
}

const C = A || B; // merging to make a new error type

void!C foo() {
    return error.NotDir;
}

test "merge error sets" {
    if (foo()) { // error test for an errorable function call
        @panic("unexpected");
    } catch (err) switch (err) {
        case error.OutOfMemory: @panic("unexpected");
        case error.PathNotFound:  @panic("unexpected");
        case error.NotDir: {};
    }
}
```
  
**Zig**
```
const A = error{
    NotDir,

    /// A doc comment
    PathNotFound,
};
const B = error{
    OutOfMemory,

    /// B doc comment
    PathNotFound,
};

const C = A || B;

fn foo() C!void {
    return error.NotDir;
}

test "merge error sets" {
    if (foo()) {
        @panic("unexpected");
    } else |err| switch (err) {
        error.OutOfMemory => @panic("unexpected"),
        error.PathNotFound => @panic("unexpected"),
        error.NotDir => {},
    }
}
```
  

## Inferred error type
  
**ZigC**
```
// With an inferred error set
public T! add_inferred(comptime type T, T a, T b) {
    T answer = undefined;
    return if (@addWithOverflow(T, a, b, &answer)) error.Overflow else answer;
}

// With an explicit error set
public T!Error add_explicit(comptime type T, T a, T b) {
    T answer = undefined;
    return if (@addWithOverflow(T, a, b, &answer)) error.Overflow else answer;
}

error Error {
    Overflow,
}

import std;

test "inferred error set" {
    if (add_inferred(u8, 255, 1) '_) unreachable catch(err) switch (err) {
        case error.Overflow: {}; // ok
    }
}
```
  
**Zig**
```
// With an inferred error set
pub fn add_inferred(comptime T: type, a: T, b: T) !T {
    var answer: T = undefined;
    return if (@addWithOverflow(T, a, b, &answer)) error.Overflow else answer;
}

// With an explicit error set
pub fn add_explicit(comptime T: type, a: T, b: T) Error!T {
    var answer: T = undefined;
    return if (@addWithOverflow(T, a, b, &answer)) error.Overflow else answer;
}

const Error = error {
    Overflow,
};

const std = @import("std");

test "inferred error set" {
    if (add_inferred(u8, 255, 1)) |_| unreachable else |err| switch (err) {
        error.Overflow => {}, // ok
    }
}
```
  


  


  
